buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "com.google.cloud.tools:appengine-gradle-plugin:+"
  }
}

plugins {
  id 'org.springframework.boot' version '1.5.7.RELEASE'
  id 'com.moowork.node' version '1.2.0'
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse'
apply plugin: 'com.google.cloud.tools.appengine'

group = 'jp.co.esm.bento'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext {
  vueDir = "${project.projectDir}/vue"
  vueDistDir = "${vueDir}/dist"
}

repositories {
  mavenCentral()
}

appengine {
  deploy {
    project 'bento-184202'
  }
  run {
    jvmFlags = ['-Ddatastore.backing_store="datastore/local_db.bin"']
  }
}
configurations {
  compile.exclude module: 'jul-to-slf4j'
}

dependencies {
  compile 'org.springframework.boot:spring-boot-starter-web'
  compile 'org.springframework.boot:spring-boot-starter-security'
  providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
  compile 'com.google.appengine:appengine-api-1.0-sdk:+'
  compileOnly 'org.projectlombok:lombok'
  compileOnly 'javax.servlet:javax.servlet-api:3.1.0'
  compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.8.10'
  compile 'com.fasterxml.jackson.core:jackson-databind:2.8.8.1'
  testCompile 'org.springframework.boot:spring-boot-starter-test'
  testCompile  'com.google.appengine:appengine-api-stubs:+'
  testCompile  'com.google.appengine:appengine-testing:+'
}

node {
  workDir = file(vueDir)
  nodeModulesDir = file(vueDir)
}

// vue„Åßnpm run build
task vue(type: NpmTask, dependsOn: npmInstall) {
  inputs.dir file("${vueDir}/src")
  inputs.dir file("${vueDir}/config")
  inputs.dir file("${vueDir}/build")
  inputs.file file("${vueDir}/package.json")
  outputs.dir file(vueDistDir)

  args = ['run', 'build']
}
classes.dependsOn vue

war {
  from vueDistDir
}

bootRepackage {
  enabled = false
}
